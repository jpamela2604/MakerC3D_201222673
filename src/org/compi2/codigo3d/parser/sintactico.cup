package org.compi2.codigo3d.parser;

import java_cup.runtime.Symbol;

//PARSER PARA EL ARCHIVO DE ESCENARIOS
parser code
{://Codigo visible

    public void syntax_error(Symbol s){
        System.err.println(s);
    }

    public void unrecovered_syntax_error(Symbol s) throws java.lang.Exception{ 
        System.err.println("T.T");
        System.err.println(s);
    }

:}

action code
{://Codigo de acciones
      
:}

terminal String ID, ENTERO, DECIMAL;
terminal TokenC3D MAS, MENOS, MULT, DIV, POT, PAR_IZQ, PAR_DER,MENOR,MAYOR,MENOR_IGUAL,MAYOR_IGUAL,IGUAL,DIFERENTE,OR,AND,NOT;
nonterminal NodoC3D inicial, expresion,logica,rela;
nonterminal String operador;


precedence left OR;
precedence left AND;
precedence left NOT;
precedence left MENOR,MAYOR,MENOR_IGUAL,MAYOR_IGUAL,IGUAL,DIFERENTE;
precedence left MAS, MENOS;
precedence left MULT, DIV; 
precedence right POT; 

start with inicial;

inicial     ::= {: ControlC3D.reiniciar(); :} logica:exp 
                    {: 
                        RESULT = exp;
                      //  ControlC3D.agregarC3D("//ltimo valor: " + exp.getCad());

                        //
                       System.out.println("//ULTIMO VALOR: "+exp.getCad());
                       ControlC3D.agregarC3D("//ULTIMO VALOR: "+exp.getCad());
                    :};

logica      ::=  logica:izq AND {:System.out.println(izq.getEV()+":\n");:} logica:der
                {: 
                        String EV = der.getEV();
                        String EF = izq.getEF()+","+der.getEF();
                        System.out.println("//Etiqueta(s) de verdadero: "+EV);
                        System.out.println("//Etiqueta(s) de falso: "+EF);
                        String c3d = "//Etiqueta(s) de verdadero: "+EV+"\n"+"//Etiqueta(s) de falso: "+EF+"\n";
                        ControlC3D.agregarC3D(c3d);
                        RESULT = new NodoC3D(EV,EF,der.getCad());
                :}
                |logica:izq OR{:System.out.println(izq.getEF()+":\n");:} logica:der
                {: 
                        String EV = izq.getEV()+","+der.getEV();
                        String EF = der.getEF();
                        System.out.println("//Etiqueta(s) de verdadero: "+EV);
                        System.out.println("//Etiqueta(s) de falso: "+EF);
                        String c3d = "//Etiqueta(s) de verdadero: "+EV+"\n"+"//Etiqueta(s) de falso: "+EF+"\n";
                        ControlC3D.agregarC3D(c3d);
                        RESULT = new NodoC3D(EV,EF,der.getCad());
                :}
                |NOT logica:der
                 {: 
                        String EV = der.getEF();
                        String EF = der.getEV();
                        
                        System.out.println("//Etiqueta(s) de verdadero: "+EV);
                        System.out.println("//Etiqueta(s) de falso: "+EF);
                        String c3d = "//Etiqueta(s) de verdadero: "+EV+"\n"+"//Etiqueta(s) de falso: "+EF+"\n";
                        ControlC3D.agregarC3D(c3d);
                        RESULT = new NodoC3D(EV,EF,der.getCad());
                :}
                |rela:der
                 {: 
                       
                        String EV = der.getEV();
                        String EF = der.getEF();
                        RESULT = new NodoC3D(EV,EF,der.getCad());
                :}
                ;
rela        ::= expresion:izq operador:op expresion:der 
                {: 
                        String EV = ControlC3D.generaEtiqueta();
                        String EF = ControlC3D.generaEtiqueta();
                       // String c3d = temp + " = " + izq.getCad() + op + der.getCad() + ";\n";
                        String c3d = "if ("+izq.getCad() + op + der.getCad() +") goto " +EV+ ";\n" +"goto "+EF+"\n";
                        System.out.println(c3d);
                        ControlC3D.agregarC3D(c3d);
                        // ControlC3D.agregarC3D(c3d);
                        RESULT = new NodoC3D(EV,EF,der.getCad()); 
                :}
                ;
operador    ::= MENOR {: RESULT="<" ; :}
                |MAYOR {: RESULT=">";  :}
                |MENOR_IGUAL {:RESULT="<="; :}
                |MAYOR_IGUAL {:RESULT=">="; :}
                |IGUAL {:RESULT="=="; :}
                ;
expresion   ::= expresion:izq MAS expresion:der
                    {: 
                        String temp = ControlC3D.generaTemp();
                        String c3d = temp + " = " + izq.getCad() + " + " + der.getCad() + ";\n";
                        System.out.println(c3d);
                        ControlC3D.agregarC3D(c3d);
                        RESULT = new NodoC3D(temp); 
                    :}
            |   expresion:izq MENOS expresion:der
                    {: 
                        String temp = ControlC3D.generaTemp();
                        String c3d = temp + " = " + izq.getCad() + " - " + der.getCad() + ";\n";
                        System.out.println(c3d); 
                        ControlC3D.agregarC3D(c3d);
                        RESULT = new NodoC3D(temp); 
                    :}
            |   expresion:izq MULT expresion:der
                    {: 
                        String temp = ControlC3D.generaTemp();
                        String c3d = temp + " = " + izq.getCad() + " * " + der.getCad() + ";\n";
                         System.out.println(c3d);
                        ControlC3D.agregarC3D(c3d);
                        RESULT = new NodoC3D(temp); 
                    :}
            |   expresion:izq DIV expresion:der
                    {: 
                        String temp = ControlC3D.generaTemp();
                        String c3d = temp + " = " + izq.getCad() + " / " + der.getCad() + ";\n";
                         System.out.println(c3d);
                        ControlC3D.agregarC3D(c3d);
                        RESULT = new NodoC3D(temp); 
                    :}
            |   expresion:izq POT expresion:der
                    {: 
                        String temp = ControlC3D.generaTemp();
                        String c3d = temp + " = " + izq.getCad() + " ^ " + der.getCad() + ";\n";
                         System.out.println(c3d);
                       ControlC3D.agregarC3D(c3d);
                        RESULT = new NodoC3D(temp); 
                    :}
            |   MENOS expresion:exp 
                    {: 
                        String temp = ControlC3D.generaTemp();
                        String c3d = temp + " = - " + exp.getCad() + ";\n";
                         System.out.println(c3d);
                        ControlC3D.agregarC3D(c3d);
                        RESULT = new NodoC3D(temp); 
                    :}
            |   PAR_IZQ expresion:exp PAR_DER   
                    {: RESULT = exp; :}
            |   ID:id           
                    {: RESULT = new NodoC3D(id); :}
            |   ENTERO:entero   
                    {: RESULT = new NodoC3D(entero); :}
            |   DECIMAL:decimal 
                    {: RESULT = new NodoC3D(decimal); :}
            ;